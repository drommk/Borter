plugins {
    id "com.jfrog.bintray" version "1.4"
}

ext.deps = [
        rxjava  : 'io.reactivex:rxjava:1.0.14',
        logger  : ['org.slf4j:slf4j-api:1.7.12',
                   'ch.qos.logback:logback-classic:1.1.3'],
        test    : ['junit:junit:4.11',
                   'com.squareup.okhttp:mockwebserver:2.5.0',
                   'org.mockito:mockito-all:1.8.4'],
        retrofit: ['com.squareup.retrofit:retrofit:2.0.0-beta2',
                   'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2',
                   'com.squareup.retrofit:converter-gson:2.0.0-beta2']
]

subprojects {
    version = '0.2.0'
    group = 'com.drommk.borter'

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
        mavenLocal()
    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = getProperty('bintray.user')
        key = getProperty('bintray.apikey')
        pkg {
            repo = 'maven'
            publications = ['mavenJava']
            name = 'borter'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/drommk/borter.git'
        }
    }

    bintrayUpload.dependsOn test

}



